plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group "io.github.rothes.actionbarmessager"
version "1.4.1"

repositories {
    mavenCentral()

    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly(project(":FoliaWrapper"))  // Works only when targetCompatibility is set to Java17
    compileOnly(files(projectDir.path + '/libs/folia-wrapper.jar'))  // Created when Folia-Wrapper is built

    compileOnly("org.jetbrains:annotations:23.0.0")
    compileOnly("org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT")
    compileOnly("me.clip:placeholderapi:2.11.3")
    compileOnly("com.comphenix.protocol:ProtocolLib:5.0.0")
    implementation("org.bstats:bstats-bukkit:3.0.0")
    implementation("org.apache.commons:commons-lang3:3.12.0")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    options.encoding = "UTF-8"
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

processResources {
    HashMap map = new HashMap()
    map.put("projectVersionString", project.version)
    expand(map)
}

shadowJar {
    relocate("org.bstats", "io.github.rothes.actionbarmessager.libs.org.bstats")
    String fileName = "ActionBarMessager v" + project.version + ".jar"
    archiveFileName.set(fileName)

    minimize()
}

test {
    useJUnitPlatform()
}

gradle.buildFinished {
    new File(project.buildDir, "resources/").deleteDir()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}